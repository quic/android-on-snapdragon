// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.3.1' apply false
    id 'com.android.library' version '7.3.1' apply false
    id 'checkstyle'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply plugin: 'checkstyle'

    configurations {
        checkstyleConfig
    }

    dependencies {
        checkstyle 'com.puppycrawl.tools:checkstyle:10.9.3'
        checkstyleConfig 'com.puppycrawl.tools:checkstyle:10.9.3'
    }

    checkstyle {
        toolVersion '10.9.3'

        // read google_checks.xml from checkstyle jar to avoid submitting the xml
        config = resources.text.fromArchiveEntry(
                configurations.checkstyleConfig[0],
                'google_checks.xml')

        ignoreFailures = false
        showViolations = true
    }

    task checkstyleTask(type: Checkstyle) {
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        classpath = files()

    }

    afterEvaluate {
        if (project.plugins.hasPlugin('com.android.application')
                || project.plugins.hasPlugin('com.android.library')) {
            preBuild.dependsOn checkstyleTask
            check.dependsOn checkstyleTask
//            checkstyleTask.mustRunAfter assemble
        }
    }
}
